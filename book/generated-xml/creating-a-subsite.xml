<?xml version="1.0" encoding="UTF-8"?>
<!DOCTYPE article PUBLIC "-//OASIS//DTD DocBook XML V4.5//EN" "http://www.oasis-open.org/docbook/xml/4.5/docbookx.dtd">
<?asciidoc-toc?>
<?asciidoc-numbered?>

<article lang="en">
<section id="creating-a-subsite_creating_a_subsite">
<title>Creating a Subsite</title>
<simpara>How many sites provide authentication systems? Or need to provide
create-read-update-delete (CRUD) management of some objects? Or a blog? Or a
wiki?</simpara>
<simpara>The theme here is that many websites include common components that can be
reused throughout multiple sites. However, it is often quite difficult to get
code to be modular enough to be truly plug-and-play: a component will require
hooks into the routing system, usually for multiple routes, and will need some
way of sharing styling information with the master site.</simpara>
<simpara>In Yesod, the solution is subsites. A subsite is a collection of routes and
their handlers that can be easily inserted into a master site. By using type
classes, it is easy to ensure that the master site provides certain
capabilities, and to access the default site layout. And with type-safe URLs,
it&#8217;s easy to link from the master site to subsites.</simpara>
<section id="creating-a-subsite_hello_world">
<title>Hello World</title>
<simpara>Perhaps the trickiest part of writing subsites is getting started.  Let&#8217;s dive
in with a simple Hello World subsite. We need to create one module to contain
our subsite&#8217;s data types, another for the subsite&#8217;s dispatch code, and then a
final module for an application that uses the subsite.</simpara>
<note><simpara>The reason for the breakdown between the data and dispatch code is due to
something called the GHC stage restriction. This requirement makes smaller
demos a bit more verbose, but in practice, this splitting up into multiple
modules is a good practice to adhere to.</simpara></note>
<programlisting language="haskell" linenumbering="unnumbered">-- @HelloSub/Data.hs
{-# LANGUAGE QuasiQuotes     #-}
{-# LANGUAGE TemplateHaskell #-}
{-# LANGUAGE TypeFamilies    #-}
module HelloSub.Data where

import           Yesod

-- Subsites have foundations just like master sites.
data HelloSub = HelloSub

-- We have a familiar analogue from mkYesod, with just one extra parameter.
-- We'll discuss that later.
mkYesodSubData "HelloSub" [parseRoutes|
/ SubHomeR GET
|]</programlisting>
<programlisting language="haskell" linenumbering="unnumbered">-- @HelloSub.hs
{-# LANGUAGE FlexibleInstances     #-}
{-# LANGUAGE MultiParamTypeClasses #-}
{-# LANGUAGE OverloadedStrings     #-}
{-# LANGUAGE QuasiQuotes           #-}
{-# LANGUAGE TemplateHaskell       #-}
module HelloSub
    ( module HelloSub.Data
    , module HelloSub
    ) where

import           HelloSub.Data
import           Yesod

-- And we'll spell out the handler type signature.
getSubHomeR :: Yesod master =&gt; SubHandlerFor HelloSub master Html
getSubHomeR = liftHandler $ defaultLayout [whamlet|Welcome to the subsite!|]

instance Yesod master =&gt; YesodSubDispatch HelloSub master where
    yesodSubDispatch = $(mkYesodSubDispatch resourcesHelloSub)</programlisting>
<programlisting language="haskell" linenumbering="unnumbered">{-# LANGUAGE OverloadedStrings #-}
{-# LANGUAGE QuasiQuotes       #-}
{-# LANGUAGE TemplateHaskell   #-}
{-# LANGUAGE TypeFamilies      #-}
import           HelloSub
import           Yesod

-- And let's create a master site that calls it.
data Master = Master
    { getHelloSub :: HelloSub
    }

mkYesod "Master" [parseRoutes|
/ HomeR GET
/subsite SubsiteR HelloSub getHelloSub
|]

instance Yesod Master

-- Spelling out type signature again.
getHomeR :: HandlerFor Master Html
getHomeR = defaultLayout
    [whamlet|
        &lt;h1&gt;Welcome to the homepage
        &lt;p&gt;
            Feel free to visit the #
            &lt;a href=@{SubsiteR SubHomeR}&gt;subsite
            \ as well.
    |]

main = warp 3000 $ Master HelloSub</programlisting>
<simpara>This simple example actually shows most of the complications involved in
creating a subsite. Like a normal Yesod application, everything in a subsite is
centered around a foundation datatype, <literal>HelloSub</literal> in our case. We then use
<literal>mkYesodSubData</literal> to generate our subsite route data type and associated parse
and render functions.</simpara>
<simpara>On the dispatch side, we start off by defining our handler function for the <literal>SubHomeR</literal> route. You should pay special attention to the type signature on this function:</simpara>
<programlisting language="haskell" linenumbering="unnumbered">getSubHomeR :: Yesod master
            =&gt; SubHandlerFor HelloSub master Html</programlisting>
<simpara>This is the heart and soul of what a subsite is all about. All of our actions
live in this layered monad, where we have our subsite wrapping around our main
site. Given this monadic layering, it should come as no surprise that we end up
calling <literal>liftHandler</literal>. In this case, our subsite is using the master site&#8217;s
<literal>defaultLayout</literal> function to render a widget.</simpara>
<simpara>The <literal>defaultLayout</literal> function is part of the Yesod typeclass. Therefore, in
order to call it, the <literal>master</literal> type argument must be an instance of <literal>Yesod</literal>.
The advantage of this approach is that any modifications to the master site&#8217;s
<literal>defaultLayout</literal> method will automatically be reflected in subsites.</simpara>
<simpara>When we embed a subsite in our master site route definition, we need to specify
four pieces of information: the route to use as the base of the subsite (in
this case, <literal>/subsite</literal>), the constructor for the subsite routes (<literal>SubsiteR</literal>),
the subsite foundation data type (<literal>HelloSub</literal>) and a function that takes a
master foundation value and returns a subsite foundation value (<literal>getHelloSub</literal>).</simpara>
<simpara>In the definition of getHomeR, we can see how the route constructor gets used.
In a sense, <literal>SubsiteR</literal> promotes any subsite route to a master site route,
making it possible to safely link to it from any master site template.</simpara>
</section>
</section>
</article>
