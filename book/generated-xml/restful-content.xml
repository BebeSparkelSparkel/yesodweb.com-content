<?xml version="1.0" encoding="UTF-8"?>
<!DOCTYPE article PUBLIC "-//OASIS//DTD DocBook XML V4.5//EN" "http://www.oasis-open.org/docbook/xml/4.5/docbookx.dtd">
<?asciidoc-toc?>
<?asciidoc-numbered?>

<article lang="en">
<section id="restful-content_restful_content">
<title>RESTful Content</title>
<simpara>One of the stories from the early days of the web is how search engines wiped
out entire websites. When dynamic web sites were still a new concept,
developers didn&#8217;t appreciate the difference between a <literal>GET</literal> and <literal>POST</literal> request.
As a result, they created pages- accessed with the <literal>GET</literal> method- that would
delete pages. When search engines started crawling these sites, they could wipe
out all the content.</simpara>
<simpara>If these web developers had followed the HTTP spec properly, this would not
have happened. A <literal>GET</literal> request is supposed to cause no side effects (you know,
like wiping out a site). Recently, there has been a move in web development to
properly embrace Representational State Transfer, also known as REST. This
chapter describes the RESTful features in Yesod and how you can use them to
create more robust web applications.</simpara>
<section id="restful-content_request_methods">
<title>Request methods</title>
<simpara>In many web frameworks, you write one handler function per resource. In Yesod,
the default is to have a separate handler function for each request method. The
two most common request methods you will deal with in creating web sites are
<literal>GET</literal> and <literal>POST</literal>. These are the most well-supported methods in HTML, since they
are the only ones supported by web forms. However, when creating RESTful APIs,
the other methods are very useful.</simpara>
<simpara>Technically speaking, you can create whichever request methods you like, but it
is strongly recommended to stick to the ones spelled out in the HTTP spec. The
most common of these are:</simpara>
<variablelist>
<varlistentry>
<term>
<literal>GET</literal>
</term>
<listitem>
<simpara>
Read-only requests. Assuming no other changes occur on the server,
calling a <literal>GET</literal> request multiple times should result in the same response,
barring such things as "current time" or randomly assigned results.
</simpara>
</listitem>
</varlistentry>
<varlistentry>
<term>
<literal>POST</literal>
</term>
<listitem>
<simpara>
A general mutating request. A <literal>POST</literal> request should never be submitted
twice by the user. A common example of this would be to transfer funds from one
bank account to another.
</simpara>
</listitem>
</varlistentry>
<varlistentry>
<term>
<literal>PUT</literal>
</term>
<listitem>
<simpara>
Create a new resource on the server, or replace an existing one. This
method <emphasis>is</emphasis> safe to be called multiple times.
</simpara>
</listitem>
</varlistentry>
<varlistentry>
<term>
<literal>DELETE</literal>
</term>
<listitem>
<simpara>
Just like it sounds: wipe out a resource on the server. Calling
multiple times should be OK.
</simpara>
</listitem>
</varlistentry>
</variablelist>
<simpara>To a certain extent, this fits in very well with Haskell philosophy: a <literal>GET</literal>
request is similar to a pure function, which cannot have side effects. In
practice, your <literal>GET</literal> functions will probably perform <literal>IO</literal>, such as reading
information from a database, logging user actions, and so on.</simpara>
<simpara>See the routing and handlers chapter chapter for more information on the syntax
of defining handler functions for each request method.</simpara>
</section>
<section id="restful-content_representations">
<title>Representations</title>
<simpara>Suppose we have a Haskell datatype and value:</simpara>
<programlisting language="haskell" linenumbering="unnumbered">data Person = Person { name :: String, age :: Int }
michael = Person "Michael" 25</programlisting>
<simpara>We could represent that data as HTML:</simpara>
<programlisting language="html" linenumbering="unnumbered">&lt;table&gt;
    &lt;tr&gt;
        &lt;th&gt;Name&lt;/th&gt;
        &lt;td&gt;Michael&lt;/td&gt;
    &lt;/tr&gt;
    &lt;tr&gt;
        &lt;th&gt;Age&lt;/th&gt;
        &lt;td&gt;25&lt;/td&gt;
    &lt;/tr&gt;
&lt;/table&gt;</programlisting>
<simpara>or we could represent it as JSON:</simpara>
<programlisting language="json" linenumbering="unnumbered">{"name":"Michael","age":25}</programlisting>
<simpara>or as XML:</simpara>
<programlisting language="xml" linenumbering="unnumbered">&lt;person&gt;
    &lt;name&gt;Michael&lt;/name&gt;
    &lt;age&gt;25&lt;/age&gt;
&lt;/person&gt;</programlisting>
<simpara>Often times, web applications will use a different URL to get each of these
representations; perhaps <literal>/person/michael.html</literal>, <literal>/person/michael.json</literal>, etc.
Yesod follows the RESTful principle of a single URL for each resource. So in
Yesod, all of these would be accessed from <literal>/person/michael</literal>.</simpara>
<simpara>Then the question becomes how do we determine <emphasis>which</emphasis> representation to serve.
The answer is the HTTP <literal>Accept</literal> header: it gives a prioritized list of content
types the client is expecting. Yesod provides a pair of functions to abstract
away the details of parsing that header directly, and instead allows you to
talk at a much higher level of representations.  Let&#8217;s make that last sentence
a bit more concrete with some code:</simpara>
<programlisting language="haskell" linenumbering="unnumbered">{-# LANGUAGE OverloadedStrings #-}
{-# LANGUAGE QuasiQuotes       #-}
{-# LANGUAGE TemplateHaskell   #-}
{-# LANGUAGE TypeFamilies      #-}
import           Data.Text (Text)
import           Yesod

data App = App

mkYesod "App" [parseRoutes|
/ HomeR GET
|]

instance Yesod App

getHomeR :: Handler TypedContent
getHomeR = selectRep $ do
    provideRep $ return
        [shamlet|
            &lt;p&gt;Hello, my name is #{name} and I am #{age} years old.
        |]
    provideRep $ return $ object
        [ "name" .= name
        , "age" .= age
        ]
  where
    name = "Michael" :: Text
    age = 28 :: Int

main :: IO ()
main = warp 3000 App</programlisting>
<simpara>The <literal>selectRep</literal> function says &#8220;I&#8217;m about to give you some possible
representations&#8221;. Each <literal>provideRep</literal> call provides an alternate representation.
Yesod uses the Haskell types to determine the mime type for each
representation. Since <literal>shamlet</literal> (a.k.a. simple Hamlet) produces an <literal>Html</literal>
value, Yesod can determine that the relevant mime type is <literal>text/html</literal>.
Similarly, <literal>object</literal> generates a JSON value, which implies the mime type
<literal>application/json</literal>. <literal>TypedContent</literal> is a data type provided by Yesod for some
raw content with an attached mime type. We&#8217;ll cover it in more detail in a
little bit.</simpara>
<simpara>To test this out, start up the server and then try running the following
different <literal>curl</literal> commands:</simpara>
<programlisting language="shell" linenumbering="unnumbered">curl http://localhost:3000 --header "accept: application/json"
curl http://localhost:3000 --header "accept: text/html"
curl http://localhost:3000</programlisting>
<simpara>Notice how the response changes based on the accept header value. Also, when
you leave off the header, the HTML response is displayed by default. The rule
here is that if there is no accept header, the first representation is
displayed. If an accept header is present, but we have no matches, then a 406
"not acceptable" response is returned.</simpara>
<simpara>By default, Yesod provides a convenience middleware that lets you set the
accept header via a query string parameter. This can make it easier to test
from your browser. To try this out, you can visit
<ulink url="http://localhost:3000/?_accept=application/json">http://localhost:3000/?_accept=application/json</ulink>.</simpara>
<simpara>Notice that we ignore entirely the list of expected content types. A number of the built in representations (<literal>RepHtml</literal>, <literal>RepPlain</literal>, <literal>RepJson</literal>, <literal>RepXml</literal>) in fact only support a single representation, and therefore what the client requests in the <literal>Accept</literal> header is irrelevant.</simpara>
<section id="restful-content_json_conveniences">
<title>JSON conveniences</title>
<simpara>Since JSON is such a commonly used data format in web applications today, we
have some built-in helper functions for providing JSON representations. These
are built off of the wonderful <literal>aeson</literal> library, so let&#8217;s start off with a quick
explanation of how that library works.</simpara>
<simpara><literal>aeson</literal> has a core datatype, <literal>Value</literal>, which represents any valid JSON value. It
also provides two typeclasses- <literal>ToJSON</literal> and <literal>FromJSON</literal>- to automate
marshaling to and from JSON values, respectively. For our purposes, we&#8217;re
currently interested in <literal>ToJSON</literal>. Let&#8217;s look at a quick example of creating a
<literal>ToJSON</literal> instance for our ever-recurring <literal>Person</literal> data type examples.</simpara>
<programlisting language="haskell" linenumbering="unnumbered">{-# LANGUAGE OverloadedStrings #-}
{-# LANGUAGE RecordWildCards   #-}
import           Data.Aeson
import qualified Data.ByteString.Lazy.Char8 as L
import           Data.Text                  (Text)

data Person = Person
    { name :: Text
    , age  :: Int
    }

instance ToJSON Person where
    toJSON Person {..} = object
        [ "name" .= name
        , "age"  .= age
        ]

main :: IO ()
main = L.putStrLn $ encode $ Person "Michael" 28</programlisting>
<simpara>I won&#8217;t go into further detail on <literal>aeson</literal>, as
<ulink url="https://www.fpcomplete.com/haddocks/aeson">the Haddock documentation</ulink>
already provides a great introduction to the library. What I&#8217;ve described so
far is enough to understand our convenience functions.</simpara>
<simpara>Let&#8217;s suppose that you have such a <literal>Person</literal> datatype, with a corresponding
value, and you&#8217;d like to use it as the representation for your current page.
For that, you can use the <literal>returnJson</literal> function.</simpara>
<programlisting language="haskell" linenumbering="unnumbered">{-# LANGUAGE OverloadedStrings #-}
{-# LANGUAGE QuasiQuotes       #-}
{-# LANGUAGE RecordWildCards   #-}
{-# LANGUAGE TemplateHaskell   #-}
{-# LANGUAGE TypeFamilies      #-}
import           Data.Text (Text)
import           Yesod

data Person = Person
    { name :: Text
    , age  :: Int
    }

instance ToJSON Person where
    toJSON Person {..} = object
        [ "name" .= name
        , "age"  .= age
        ]

data App = App

mkYesod "App" [parseRoutes|
/ HomeR GET
|]

instance Yesod App

getHomeR :: Handler Value
getHomeR = returnJson $ Person "Michael" 28

main :: IO ()
main = warp 3000 App</programlisting>
<simpara><literal>returnJson</literal> is actually a trivial function; it is implemented as <literal>return .
toJSON</literal>. However, it makes things just a bit more convenient. Similarly, if you
would like to provide a JSON value as a representation inside a <literal>selectRep</literal>,
you can use <literal>provideJson</literal>.</simpara>
<programlisting language="haskell" linenumbering="unnumbered">{-# LANGUAGE OverloadedStrings #-}
{-# LANGUAGE QuasiQuotes       #-}
{-# LANGUAGE RecordWildCards   #-}
{-# LANGUAGE TemplateHaskell   #-}
{-# LANGUAGE TypeFamilies      #-}
import           Data.Text (Text)
import           Yesod

data Person = Person
    { name :: Text
    , age  :: Int
    }

instance ToJSON Person where
    toJSON Person {..} = object
        [ "name" .= name
        , "age"  .= age
        ]

data App = App

mkYesod "App" [parseRoutes|
/ HomeR GET
|]

instance Yesod App

getHomeR :: Handler TypedContent
getHomeR = selectRep $ do
    provideRep $ return
        [shamlet|
            &lt;p&gt;Hello, my name is #{name} and I am #{age} years old.
        |]
    provideJson person
  where
    person@Person {..} = Person "Michael" 28

main :: IO ()
main = warp 3000 App</programlisting>
<simpara><literal>provideJson</literal> is similarly trivial, in this case <literal>provideRep . returnJson</literal>.</simpara>
</section>
<section id="restful-content_new_datatypes">
<title>New datatypes</title>
<simpara>Let&#8217;s say I&#8217;ve come up with some new data format based on using Haskell&#8217;s
<literal>Show</literal> instance; I&#8217;ll call it &#8220;Haskell Show&#8221;, and give it a mime type of
<literal>text/haskell-show</literal>. And let&#8217;s say that I decide to include this representation
from my web app. How do I do it? For a first attempt, let&#8217;s use the
<literal>TypedContent</literal> datatype directly.</simpara>
<programlisting language="haskell" linenumbering="unnumbered">{-# LANGUAGE OverloadedStrings #-}
{-# LANGUAGE QuasiQuotes       #-}
{-# LANGUAGE TemplateHaskell   #-}
{-# LANGUAGE TypeFamilies      #-}
import           Data.Text (Text)
import           Yesod

data Person = Person
    { name :: Text
    , age  :: Int
    }
    deriving Show

data App = App

mkYesod "App" [parseRoutes|
/ HomeR GET
|]

instance Yesod App

mimeType :: ContentType
mimeType = "text/haskell-show"

getHomeR :: Handler TypedContent
getHomeR =
    return $ TypedContent mimeType $ toContent $ show person
  where
    person = Person "Michael" 28

main :: IO ()
main = warp 3000 App</programlisting>
<simpara>There are a few important things to note here.</simpara>
<itemizedlist>
<listitem>
<simpara>
We&#8217;ve used the <literal>toContent</literal> function. This is a typeclass function that can
  convert a number of data types to raw data ready to be sent over the wire. In
  this case, we&#8217;ve used the instance for <literal>String</literal>, which uses UTF8 encoding.
  Other common data types with instances are <literal>Text</literal>, <literal>ByteString</literal>, <literal>Html</literal>, and
  aeson&#8217;s <literal>Value</literal>.
</simpara>
</listitem>
<listitem>
<simpara>
We&#8217;re using the <literal>TypedContent</literal> constructor directly. It takes two arguments:
  a mime type, and the raw content. Note that <literal>ContentType</literal> is simply a type
  alias for a strict <literal>ByteString</literal>.
</simpara>
</listitem>
</itemizedlist>
<simpara>That&#8217;s all well and good, but it bothers me that the type signature for
<literal>getHomeR</literal> is so uninformative. Also, the implementation of <literal>getHomeR</literal> looks
pretty boilerplate. I&#8217;d rather just have a datatype representing "Haskell Show"
data, and provide some simple means of creating such values. Let&#8217;s try this on
for size:</simpara>
<programlisting language="haskell" linenumbering="unnumbered">{-# LANGUAGE ExistentialQuantification #-}
{-# LANGUAGE OverloadedStrings         #-}
{-# LANGUAGE QuasiQuotes               #-}
{-# LANGUAGE TemplateHaskell           #-}
{-# LANGUAGE TypeFamilies              #-}
import           Data.Text (Text)
import           Yesod

data Person = Person
    { name :: Text
    , age  :: Int
    }
    deriving Show

data App = App

mkYesod "App" [parseRoutes|
/ HomeR GET
|]

instance Yesod App

mimeType :: ContentType
mimeType = "text/haskell-show"

data HaskellShow = forall a. Show a =&gt; HaskellShow a

instance ToContent HaskellShow where
    toContent (HaskellShow x) = toContent $ show x
instance ToTypedContent HaskellShow where
    toTypedContent = TypedContent mimeType . toContent

getHomeR :: Handler HaskellShow
getHomeR =
    return $ HaskellShow person
  where
    person = Person "Michael" 28

main :: IO ()
main = warp 3000 App</programlisting>
<simpara>The magic here lies in two typeclasses. As we mentioned before, <literal>ToContent</literal>
tells how to convert a value into a raw response. In our case, we would like to
<literal>show</literal> the original value to get a <literal>String</literal>, and then convert that <literal>String</literal>
into the raw content. Often times, instances of <literal>ToContent</literal> will build on each
other in this way.</simpara>
<simpara><literal>ToTypedContent</literal> is used internally by Yesod, and is called on the result of
all handler functions. As you can see, the implementation is fairly trivial,
simply stating the mime type and then calling out to <literal>toContent</literal>.</simpara>
<simpara>Finally, let&#8217;s make this a bit more complicated, and get this to play well with
<literal>selectRep</literal>.</simpara>
<programlisting language="haskell" linenumbering="unnumbered">{-# LANGUAGE ExistentialQuantification #-}
{-# LANGUAGE OverloadedStrings         #-}
{-# LANGUAGE QuasiQuotes               #-}
{-# LANGUAGE RecordWildCards           #-}
{-# LANGUAGE TemplateHaskell           #-}
{-# LANGUAGE TypeFamilies              #-}
import           Data.Text (Text)
import           Yesod

data Person = Person
    { name :: Text
    , age  :: Int
    }
    deriving Show

instance ToJSON Person where
    toJSON Person {..} = object
        [ "name" .= name
        , "age"  .= age
        ]

data App = App

mkYesod "App" [parseRoutes|
/ HomeR GET
|]

instance Yesod App

mimeType :: ContentType
mimeType = "text/haskell-show"

data HaskellShow = forall a. Show a =&gt; HaskellShow a

instance ToContent HaskellShow where
    toContent (HaskellShow x) = toContent $ show x
instance ToTypedContent HaskellShow where
    toTypedContent = TypedContent mimeType . toContent
instance HasContentType HaskellShow where
    getContentType _ = mimeType

getHomeR :: Handler TypedContent
getHomeR = selectRep $ do
    provideRep $ return $ HaskellShow person
    provideJson person
  where
    person = Person "Michael" 28

main :: IO ()
main = warp 3000 App</programlisting>
<simpara>The important addition here is the <literal>HasContentType</literal> instance. This may seem
redundant, but it serves an important role. We need to be able to determine the
mime type of a possible representation <emphasis>before creating that representation</emphasis>.
<literal>ToTypedContent</literal> only works on a concrete value, and therefore can&#8217;t be used
before creating the value. <literal>getContentType</literal> instead takes a proxy value,
indicating the type without providing anything concrete.</simpara>
<note><simpara>If you want to provide a representation for a value that doesn&#8217;t have a
<literal>HasContentType</literal> instance, you can use the <literal>provideRepType</literal> function, which
requires you to explicitly state the mime type present.</simpara></note>
</section>
</section>
<section id="restful-content_other_request_headers">
<title>Other request headers</title>
<simpara>There are a great deal of other request headers available. Some of them only
affect the transfer of data between the server and client, and should not
affect the application at all. For example, <literal>Accept-Encoding</literal> informs the
server which compression schemes the client understands, and <literal>Host</literal> informs the
server which virtual host to serve up.</simpara>
<simpara>Other headers <emphasis>do</emphasis> affect the application, but are automatically read by Yesod.
For example, the <literal>Accept-Language</literal> header specifies which human language
(English, Spanish, German, Swiss-German) the client prefers. See the i18n
chapter for details on how this header is used.</simpara>
</section>
<section id="restful-content_stateless">
<title>Stateless</title>
<simpara>I&#8217;ve saved this section for the last, not because it is less important, but
rather because there are no specific features in Yesod to enforce this.</simpara>
<simpara>HTTP is a stateless protocol: each request is to be seen as the beginning of a
conversation. This means, for instance, it doesn&#8217;t matter to the server if you
requested five pages previously, it will treat your sixth request as if it&#8217;s
your first one.</simpara>
<simpara>On the other hand, some features on websites won&#8217;t work without some kind of
state. For example, how can you implement a shopping cart without saving
information about items in between requests?</simpara>
<simpara>The solution to this is cookies, and built on top of this, sessions. We have a
whole section addressing the sessions features in Yesod. However, I cannot
stress enough that this should be used sparingly.</simpara>
<simpara>Let me give you an example. There&#8217;s a popular bug tracking system that I deal
with on a daily basis which horribly abuses sessions. There&#8217;s a little
drop-down on every page to select the current project. Seems harmless, right?
What that dropdown does is set the current project in your session.</simpara>
<simpara>The result of all this is that clicking on the "view issues" link is entirely
dependent on the last project you selected. There&#8217;s no way to create a bookmark
to your "Yesod" issues and a separate link for your "Hamlet" issues.</simpara>
<simpara>The proper RESTful approach to this is to have one resource for all of the
Yesod issues and a separate one for all the Hamlet issues. In Yesod, this is
easily done with a route definition like:</simpara>
<programlisting language="routes" linenumbering="unnumbered">/                    ProjectsR      GET
/projects/#ProjectID ProjectIssuesR GET
/issues/#IssueID     IssueR         GET</programlisting>
<simpara>Be nice to your users: proper stateless architecture means that basic features
like bookmarks, permalinks and the back/forward button will always work.</simpara>
</section>
<section id="restful-content_summary">
<title>Summary</title>
<simpara>Yesod adheres to the following tenets of REST:</simpara>
<itemizedlist>
<listitem>
<simpara>
Use the correct request method.
</simpara>
</listitem>
<listitem>
<simpara>
Each resource should have precisely one URL.
</simpara>
</listitem>
<listitem>
<simpara>
Allow multiple representations of data on the same URL.
</simpara>
</listitem>
<listitem>
<simpara>
Inspect request headers to determine extra information about what the client wants.
</simpara>
</listitem>
</itemizedlist>
<simpara>This makes it easy to use Yesod not just for building websites, but for
building APIs. In fact, using techniques such as <literal>selectRep</literal>/<literal>provideRep</literal>, you
can serve both a user-friendly, HTML page and a machine-friendly, JSON page
from the same URL.</simpara>
</section>
</section>
</article>
